buildscript {
    apply from: "$rootProject.projectDir/gradle/dependencies.gradle"

    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath pluginLibs.kotlinGradlePlugin
        classpath pluginLibs.dependencyManagementPlugin
        classpath pluginLibs.flywayPlugin
    }
}

apply plugin: 'kotlin'
apply plugin: "io.spring.dependency-management"
apply plugin: "org.flywaydb.flyway"

dependencyManagement {
    imports {
        mavenBom 'org.jdbi:jdbi3-bom:3.0.0-beta2'
    }
    // override old kotlin (transitive) version from jdbi3
    dependencies {
        dependency "org.jetbrains.kotlin:kotlin-stdlib:$versions.kotlin"
        dependency "org.jetbrains.kotlin:kotlin-reflect:$versions.kotlin"
    }
}

dependencies {
    compile project(':shared')
    compile project(':domain')
    compile project(':application')

    compile libs.sparkKotlin
    compile libs.sparkTemplateVelocity
    compile libs.postgresql
    compile libs.flywayCore
    compile 'org.jdbi:jdbi3'
    compile 'org.jdbi:jdbi3-kotlin'
    compile 'org.jdbi:jdbi3-kotlin-sqlobject'
}

configurations.all {
    resolutionStrategy {
        force "org.jetbrains.kotlin:kotlin-stdlib:$versions.kotlin"
        force "org.jetbrains.kotlin:kotlin-reflect:$versions.kotlin"
    }
}

flyway {
    url = 'jdbc:postgresql://localhost:5432/pajulahti'
    user = 'postgres'
    password = 'password'
}