buildscript {
    apply from: "gradle/dependencies.gradle"
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    configurations.create("ktlint")
    dependencies {
        classpath pluginLibs.kotlinGradlePlugin
        classpath pluginLibs.ktlintPlugin
        ktlint pluginLibs.ktlint
    }
}

apply from: "gradle/ktlint.gradle"
apply plugin: "org.jlleitschuh.gradle.ktlint"

ktlint {
    verbose = true
    reporter = "checkstyle"
    ignoreFailures = true
}

allprojects {
    group 'nemit.fi'
    version '1.0-SNAPSHOT'
}

subprojects {

    apply plugin: 'kotlin'

    task allDeps(type: DependencyReportTask) {}

    repositories {
        mavenCentral()
        maven {url "https://jcenter.bintray.com"}
    }

    dependencies {
        compile libs.kotlinStdLib
        compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
        testCompile libs.junitJupiterApi
        testCompile libs.mockitoKotlin
    }

    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

    configurations
        .matching{ it.name != 'ktlint' }
        .all {
            resolutionStrategy {
                failOnVersionConflict()
                preferProjectModules()

                libs.each { k, v -> force(v) }
                force "org.jetbrains.kotlin:kotlin-stdlib:$versions.kotlin"
                force "org.jetbrains.kotlin:kotlin-reflect:$versions.kotlin"
            }
        }
}